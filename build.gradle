plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.5'
	id 'io.spring.dependency-management' version '1.1.3'
	id 'io.freefair.lombok' version '8.4'
}

group = 'com.ys'
version = '0.0.1-SNAPSHOT'

def javaProjects = [
		project(':infrastructure'),
		project(':domain'),
		project(':notifier'),
		project(':application'),
		project(':adapter'),
		project(':scheduler'),
]
def springBootProjects = [
		project(':notifier'),
		project(':application'),
		project(':adapter'),
		project(':scheduler')
]
def jpaProjects = [
		project(':application'),
		project(':adapter')
]

configure(javaProjects) {
	apply plugin: "java"
	apply plugin: "io.freefair.lombok"

	tasks.withType(JavaCompile) {
		sourceCompatibility = "17"
		targetCompatibility = "17"
		options.encoding = "UTF-8"
	}

	dependencies {
		implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
		implementation 'org.hibernate:hibernate-validator:7.0.5.Final'
		implementation 'org.glassfish:jakarta.el:4.0.2'
		implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
		implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
		implementation 'com.fasterxml.uuid:java-uuid-generator:4.1.0'
		implementation 'org.apache.commons:commons-lang3:3.13.0'
		implementation 'commons-validator:commons-validator:1.7'

		if (!springBootProjects.contains(project)) {
			testImplementation(platform('org.junit:junit-bom:5.7.1'))
			testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
			testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.3'
			testImplementation 'org.assertj:assertj-core:3.20.2'
			testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
		}
	}
}

configure(springBootProjects) {
	apply plugin: "org.springframework.boot"
	apply plugin: "io.spring.dependency-management"

	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-validation'
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.slf4j:slf4j-api:2.0.7'

		testImplementation "org.springframework.boot:spring-boot-starter-test"
		testImplementation "org.mybatis.spring.boot:mybatis-spring-boot-starter-test:3.0.2"
		testImplementation 'com.github.tomakehurst:wiremock-standalone:2.27.2'
	}
}

configure(jpaProjects) {
	dependencies {
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	}
}

allprojects {
	repositories {
		mavenCentral()
	}
}

bootJar {
	enabled(false)
}

tasks.named('test') {
	useJUnitPlatform()
}
